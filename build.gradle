// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.robolectric_version = "4.7.3"
    ext.hilt_version = "2.42" // 2.38.1
    ext.data_binding_compiler = "7.1.3"
    ext.nav_component_version = "2.4.2"
    ext.greencoffee_version = "3.5.0"
    ext.uiautomator_version = "2.2.0"
    ext.lifecycle_version = "2.4.0-alpha02"

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.1.3"
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10'
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task unitTests {
    doLast {
        subprojects.forEach {
            def path = it.path

            // Run unit test for a java library/kotlin module
            if ((it.plugins.findPlugin('java-library') != null) && it.name != "featureTesting") {
                exec {
                    executable './gradlew'
                    args "${path}:test"
                }
            }

            // Run unit test for android library module
            if ((it.plugins.findPlugin('com.android.library') != null) && it.name != "featureTesting") {
                exec {
                    executable './gradlew'
                    args "${path}:testDebugUnitTest"
                }
            }
        }
    }
}

task featureTests {
    doLast {
        subprojects.forEach {
            def path = it.path

            // Run integration test
            if (it.name == "featureTesting") {
                exec {
                    executable './gradlew'
                    args "${path}:testDebugUnitTest"

                }
            }
        }
    }
}